# Due to the lack of an overloaded `safeBatchTransferFrom` function for transferring game items under any circumstance.


## Impact
Due to the lack of overloading `safeBatchTransferFrom`, even if an administrator sets the 'transferable' attribute of a game item to false, players can still transfer this type of game item arbitrarily using `safeBatchTransferFrom`.

## Proof of Concept
The reason for designing a game item structure with a "transferable" attribute corresponding to an ERC1155 token is to restrict whether this type of token can be transferred to others. This design anticipates future scenarios where certain game items, such as weapons bound to a character's soul, may need to be restricted from transfer.  
The "GameItems" contract overrides the `safeTransferFrom` function, checking whether an item is transferable before calling the parent contract's safeTransferFrom.  
Github:[[291](https://github.com/code-423n4/2024-02-ai-arena/blob/cd1a0e6d1b40168657d1aaee8223dc050e15f8cc/src/GameItems.sol#L291)]
```solidity
    function safeTransferFrom(
        address from, 
        address to, 
        uint256 tokenId,
        uint256 amount,
        bytes memory data
    ) 
        public 
        override(ERC1155)
    {
        require(allGameItemAttributes[tokenId].transferable);
        super.safeTransferFrom(from, to, tokenId, amount, data);
    }
```
  
But the ERC1155 standard also provides a batch transfer function `safeBatchTransferFrom`, which has not been overridden to accommodate the transferable attribute limitation.Its implementation has no effect on the implementation of the `safeTransferFrom` function.  
GitHub:[[134](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/ecd2ca2cd7cac116f7a37d0e474bbb3d7d5e1c4d/contracts/token/ERC1155/ERC1155.sol#L134)]
```solidity
 function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) public virtual override {
        require(
            from == _msgSender() || isApprovedForAll(from, _msgSender()),
            "ERC1155: caller is not token owner nor approved"
        );
        _safeBatchTransferFrom(from, to, ids, amounts, data);
    }
```

So at any time, players can transfer game items using `safeBatchTransferFrom`.  

#### POC
If the game item with token ID 0 is set to non-transferable, using `safeTransferFrom` fails, but using `safeBatchTransferFrom` succeeds.  
```solidity
    function testSafeTransferFrom() public {
        _fundUserWith4kNeuronByTreasury(_ownerAddress);
        _gameItemsContract.adjustTransferability(0,false);
        _gameItemsContract.mint(0, 1);

        vm.expectRevert();
        _gameItemsContract.safeTransferFrom(_ownerAddress, _DELEGATED_ADDRESS, 0, 1, "");
        console.log("SafeTransferFrom Failed!");

        uint256[] memory ids = new uint256[](1);
        uint256[] memory amount = new uint256[](1);
        amount[0] =1;
        _gameItemsContract.safeBatchTransferFrom(_ownerAddress, _DELEGATED_ADDRESS,ids, amount, "");
        console.log("SafeBatchTransferFrom Success!");

        assertEq(_gameItemsContract.balanceOf(_DELEGATED_ADDRESS, 0), 1);
        assertEq(_gameItemsContract.balanceOf(_ownerAddress, 0), 0);
        console.log("State Check Success!");
    }
```
result  
```solidity
[PASS] testSafeTransferFrom() (gas: 190411)
Logs:
  SafeTransferFrom Failed!
  SafeBatchTransferFrom Success!
  State Check Success!

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 3.65ms
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Tools Used
Manual audit,foundry  
  
## Recommended Mitigation Steps
Overloading the `safeBatchTransferFrom` function.
```solidity
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) public override(ERC1155) {
        require(ids.length == amounts.length);
        uint256 length = ids.length;
        for (uint256 i = 0; i < length; i++){
            if(!allGameItemAttributes[ids[i]].transferable){
                revert("restricted transfer!");
            }
        }
        super.safeBatchTransferFrom(from, to, ids, amounts, data);
    }
```
Or to directly disable `safeBatchTransferFrom`.
```solidity
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) public override {
        revert("safeBatchTransferFrom is disabled in this contract");
    }
```