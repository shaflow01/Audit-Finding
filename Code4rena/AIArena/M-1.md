

# In certain cases, the GameServer calling the updateBattleRecord function may result in revert.


## Impact
Due to the assets in stake at risk not being considered, in certain cases, the GameServer calling the updateBattleRecord function may revert.  

## Proof of Concept
A fighter can only be transferred to another person when it has not been staked with NRN.  
When we call the stakeNRN function of the RankedBattle contract for the first time in each round, it will change the FighterStaking status of the fighter to true.  
Github:[[253](https://github.com/code-423n4/2024-02-ai-arena/blob/cd1a0e6d1b40168657d1aaee8223dc050e15f8cc/src/RankedBattle.sol#L253)]

```solidity
if (amountStaked[tokenId] == 0) {
  _fighterFarmInstance.updateFighterStaking(tokenId, true);
}
```
Github:[[285](https://github.com/code-423n4/2024-02-ai-arena/blob/cd1a0e6d1b40168657d1aaee8223dc050e15f8cc/src/RankedBattle.sol#L285)]
We call the unstakeNRN function to change the FighterStaking status of the fighter to false and enable the transfer to another person when amountStaked[tokenId] equals 0.  
```solidity
  if (amountStaked[tokenId] == 0) {
      _fighterFarmInstance.updateFighterStaking(tokenId, false);
  }
```  
However, the fighter may have assets at risk.  
In that case, if the fighter is transferred to another person, then in the same round, the fighter's battle will still proceed to settle in the _addResultPoints function.  
Github:[[342](https://github.com/code-423n4/2024-02-ai-arena/blob/cd1a0e6d1b40168657d1aaee8223dc050e15f8cc/src/RankedBattle.sol#L342)]
```solidity
  if (amountStaked[tokenId] + stakeAtRisk > 0) {
      _addResultPoints(battleResult, tokenId, eloFactor, mergingPortion, fighterOwner);
  }
```
In this scenario, draws and losses in battle results can be updated normally, but victorious battle results will be revert.  
Because the amountLost in the StakeAtRisk contract may leading to underflow and causing the transaction to revert.  
Github:[[102](https://github.com/code-423n4/2024-02-ai-arena/blob/cd1a0e6d1b40168657d1aaee8223dc050e15f8cc/src/StakeAtRisk.sol#L102)]
```solidity
  stakeAtRisk[roundId][fighterId] -= nrnToReclaim;
  totalStakeAtRisk[roundId] -= nrnToReclaim;
  amountLost[fighterOwner] -= nrnToReclaim;
```
This issue will disappear once the next round begins.  

POC:
When player1 stakes the token with token ID = 0, and some assets of fighter are at risk, then player1 revokes all stakes and transfers the fighter to player2 (assuming player2 purchases the fighter), all the combat victories updates of player2 in this turn would be reverted.
However, losing and tie battles can be updated normally.
```solidity
  function testRevertupdateBattleRecord() public{
      address player = vm.addr(3);
      address player2 = vm.addr(4);
      _mintFromMergingPool(player);
      _fundUserWith4kNeuronByTreasury(player);
      _fundUserWith4kNeuronByTreasury(player2);
      vm.prank(player);
      _rankedBattleContract.stakeNRN(3_000 * 10 ** 18, 0);
      vm.prank(address(_GAME_SERVER_ADDRESS));
      _rankedBattleContract.updateBattleRecord(0, 50, 2, 1500, true);
      vm.prank(player);
      _rankedBattleContract.unstakeNRN(3_000 * 10 ** 18,0);
      console.log("token id 0 some at risk player1 unstakeNRN transfer to player2:");
      vm.prank(player);
      _fighterFarmContract.safeTransferFrom(player, player2, 0);  

      console.log("If player2 win,updateBattleRecord will be revert");
      vm.prank(address(_GAME_SERVER_ADDRESS));
      vm.expectRevert();
      _rankedBattleContract.updateBattleRecord(0, 50, 0, 1500, true);  

      vm.prank(address(_GAME_SERVER_ADDRESS));
      _rankedBattleContract.updateBattleRecord(0, 50, 2, 1500, true);

      vm.prank(address(_GAME_SERVER_ADDRESS));
      _rankedBattleContract.updateBattleRecord(0, 50, 1, 1500, true);

  }
```

result  
```solidity
Running 1 test for test/RankedBattle.t.sol:RankedBattleTest
[PASS] testRevertupdateBattleRecord() (gas: 1009702)
Logs:
token id 0 some at risk player1 unstakeNRN transfer to player2:
If player2 win,updateBattleRecord will be revert
```
## Tools Used
Manual audit,foundry
## Recommended Mitigation Steps
* Method one.
**Consider stake-at-risk assets when changing the fighter's staking status again.**  
```solidity
  if (
      amountStaked[tokenId] == 0 &&
  +     _stakeAtRiskInstance.getStakeAtRisk(tokenId) == 0
      ) {
          _fighterFarmInstance.updateFighterStaking(tokenId, false);
  }
```  

The limitation of this modification is that the player wishing to transfer the fighter must call unstakeNRN again after the start of the new round if his fighter has NRN at risk in this round.And the transferred fighter cannot participate in staking during new round.They need to wait for one more round.  

* Method two.

**Adding condition restrictions to the transfer, but it will require significant changes.**

Change in `FighterFarm.sol`  
```solidity
  + mapping(uint256 => bool) public stakeAtRisk;

  + function updateStakingAtRisk(uint256 tokenId, bool stakingStatus) external {
  +     require(hasStakerRole[msg.sender]);
  +     stakeAtRisk[tokenId] = stakingStatus;
  + }

  function _ableToTransfer(
      uint256 tokenId,
      address to
  ) private view returns (bool) {
      return (_isApprovedOrOwner(msg.sender, tokenId) &&
          balanceOf(to) < MAX_FIGHTERS_ALLOWED &&
          !fighterStaked[tokenId] 
  +        &&!stakeAtRisk[tikenId]);
  }
```
Change in `RankedBattle.sol`  
```solidity
  
  /// If the user has stake-at-risk for their fighter, reclaim a portion
  /// Reclaiming stake-at-risk puts the NRN back into their staking pool
  if (curStakeAtRisk > 0) {
      _stakeAtRiskInstance.reclaimNRN(
          curStakeAtRisk,
          tokenId,
          fighterOwner
      );
      amountStaked[tokenId] += curStakeAtRisk;
  }
  + if (_stakeAtRiskInstance.getStakeAtRisk(tokenId) == 0) {
  +     _fighterFarmInstance.updateStakingAtRisk(tokenId, false);
  + }
```
```solidity
  else {
      /// If the fighter does not have any points for this round, NRNs become at risk of being lost
          bool success = _neuronInstance.transfer(
          _stakeAtRiskAddress,
              curStakeAtRisk
          );
  +       if (_stakeAtRiskInstance.getStakeAtRisk(tokenId) == 0) {
  +           _fighterFarmInstance.updateStakingAtRisk(tokenId, true);
  +       }
          if (success) {
               _stakeAtRiskInstance.updateAtRiskRecords(
                  curStakeAtRisk,
                  tokenId,
                  fighterOwner
              );
              amountStaked[tokenId] -= curStakeAtRisk;
  
          }
  }
```