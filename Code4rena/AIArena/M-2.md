# The contract lacks admin roles for MINTER_ROLE, STAKER_ROLE, and PENDER_ROLE,and this leads to certain roles being unable to be revoked.

## Impact
The contract lacks admin roles for MINTER_ROLE, STAKER_ROLE, and PENDER_ROLE, which means roles cannot be revoked by anyone other than themselves. Due to the absence of a function in the role contracts to relinquish privileges, the old privileged roles cannot be revoked.If these roles are compromised, further losses cannot be prevented by revoking their authorization.  

## Proof of Concept

First of all, I want to say that roles such as MINTER_ROLE and STAKER_ROLE in the NRN token contract have significant permissions. They can control any account to perform approve operations on themselves, thus transferring their tokens.  

Github:[[171](https://github.com/code-423n4/2024-02-ai-arena/blob/f2952187a8afc44ee6adc28769657717b498b7d4/src/Neuron.sol#L171)]

```solidity
    function approveSpender(address account, uint256 amount) public {
        require(
            hasRole(SPENDER_ROLE, msg.sender), 
            "ERC20: must have spender role to approve spending"
        );
        _approve(account, msg.sender, amount);
    }

    /// @notice Approves the specified amount of tokens for the staker address.
    /// @dev The caller must have the staker role.
    /// @param owner The owner of the tokens.
    /// @param spender The address for which to approve the allowance.
    /// @param amount The amount of tokens to be approved.
    function approveStaker(address owner, address spender, uint256 amount) public {
        require(
            hasRole(STAKER_ROLE, msg.sender), 
            "ERC20: must have staker role to approve staking"
        );
        _approve(owner, spender, amount);
    }
```

The NRN contract inherits the AccessControl contract, in which three functions are implemented to add these roles. They can only be called by the owner.  

Github:[[101](https://github.com/code-423n4/2024-02-ai-arena/blob/f2952187a8afc44ee6adc28769657717b498b7d4/src/Neuron.sol#L101)]
```solidity
    /// @notice Adds a new address to the minter role.
    /// @dev Only the owner address is authorized to call this function.
    /// @param newMinterAddress The address to be added as a minter
    function addMinter(address newMinterAddress) external {
        require(msg.sender == _ownerAddress);
        _setupRole(MINTER_ROLE, newMinterAddress);
    }

    /// @notice Adds a new address to the staker role.
    /// @dev Only the owner address is authorized to call this function.
    /// @param newStakerAddress The address to be added as a staker
    function addStaker(address newStakerAddress) external {
        require(msg.sender == _ownerAddress);
        _setupRole(STAKER_ROLE, newStakerAddress);
    }

    /// @notice Adds a new address to the spender role.
    /// @dev Only the owner address is authorized to call this function.
    /// @param newSpenderAddress The address to be added as a spender
    function addSpender(address newSpenderAddress) external {
        require(msg.sender == _ownerAddress);
        _setupRole(SPENDER_ROLE, newSpenderAddress);
    }

```
It is important to note that when the owner calls these functions again to add new roles, the existing roles will not be overwritten, and their permissions will not be revoked.Because it only sets the relevant mapping for the new address to true.  

```solidity
    function _grantRole(bytes32 role, address account) internal virtual {
        if (!hasRole(role, account)) {
            _roles[role].members[account] = true;
            emit RoleGranted(role, account, _msgSender());
        }
    }
```


How can authorization for the original role be revoked?   
  
Upon observing the NRN contract, I noticed that it does not encapsulate the _setRoleAdmin(bytes32 role, bytes32 adminRole) function for administrator setting, resulting in the inability to add administrators, and the owner is not the default_admin_role either.  
Therefore, others cannot revoke authorization for roles that have already been granted. The only way to revoke authorization is for these roles to call the renounceRole method themselves.   
However, these contract roles such as GameItems.sol, FighterFarm.sol, and RankedBattle.sol do not implement the relevant functions, preventing authorized addresses from revoking authorization.  
  
### POC:

```solidity
    function testRoleRevork() public {
        
        _neuronContract.addSpender(_DELEGATED_ADDRESS);
        assertEq(_neuronContract.hasRole(keccak256("SPENDER_ROLE"), _DELEGATED_ADDRESS), true);

        address newSpenderAddress = makeAddr("newSpenderAddress");
        _neuronContract.addSpender(newSpenderAddress);
        assertEq(_neuronContract.hasRole(keccak256("SPENDER_ROLE"), newSpenderAddress), true);
        assertEq(_neuronContract.hasRole(keccak256("SPENDER_ROLE"), _DELEGATED_ADDRESS), true);

        vm.expectRevert();
        _neuronContract.revokeRole(keccak256("SPENDER_ROLE"),_DELEGATED_ADDRESS);
        
    }
```
result
```solidity
Running 1 test for test/Neuron.t.sol:NeuronTest
[PASS] testRoleRevork() (gas: 101536)
Traces:
  [101536] NeuronTest::testRoleRevork()
    ├─ [27180] Neuron::addSpender(0x22F4441ad6DbD602dFdE5Cd8A38F6CAdE68860b0)
    │   ├─ emit RoleGranted(role: 0x7434c6f201a551bfd17336985361933e0c4935b520dac8a49d937b325f7d5c0a, account: 0x22F4441ad6DbD602dFdE5Cd8A38F6CAdE68860b0, sender: NeuronTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496])
    │   └─ ← ()
    ├─ [721] Neuron::hasRole(0x7434c6f201a551bfd17336985361933e0c4935b520dac8a49d937b325f7d5c0a, 0x22F4441ad6DbD602dFdE5Cd8A38F6CAdE68860b0) [staticcall]
    │   └─ ← true
    ├─ [0] VM::addr(<pk>) [staticcall]
    │   └─ ← newSpenderAddress: [0xFB497e8C257f47A0623E72e5430753E4DFb0643B]
    ├─ [0] VM::label(newSpenderAddress: [0xFB497e8C257f47A0623E72e5430753E4DFb0643B], newSpenderAddress)
    │   └─ ← ()
    ├─ [25180] Neuron::addSpender(newSpenderAddress: [0xFB497e8C257f47A0623E72e5430753E4DFb0643B])
    │   ├─ emit RoleGranted(role: 0x7434c6f201a551bfd17336985361933e0c4935b520dac8a49d937b325f7d5c0a, account: newSpenderAddress: [0xFB497e8C257f47A0623E72e5430753E4DFb0643B], sender: NeuronTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496])
    │   └─ ← ()
    ├─ [721] Neuron::hasRole(0x7434c6f201a551bfd17336985361933e0c4935b520dac8a49d937b325f7d5c0a, newSpenderAddress: [0xFB497e8C257f47A0623E72e5430753E4DFb0643B]) [staticcall]
    │   └─ ← true
    ├─ [721] Neuron::hasRole(0x7434c6f201a551bfd17336985361933e0c4935b520dac8a49d937b325f7d5c0a, 0x22F4441ad6DbD602dFdE5Cd8A38F6CAdE68860b0) [staticcall]
    │   └─ ← true
    ├─ [0] VM::expectRevert()
    │   └─ ← ()
    ├─ [34479] Neuron::revokeRole(0x7434c6f201a551bfd17336985361933e0c4935b520dac8a49d937b325f7d5c0a, 0x22F4441ad6DbD602dFdE5Cd8A38F6CAdE68860b0)
    │   └─ ← "AccessControl: account 0x7fa9385be102ac3eac297483dd6233d62b3e1496 is missing role 0x0000000000000000000000000000000000000000000000000000000000000000"
    └─ ← ()

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 10.88ms
```

## Tools Used
  
Manual audit,foundry
## Recommended Mitigation Steps
The most convenient method is to add the owner as the default_admin_role role. If you want to implement more complex management permissions, please read the relevant comments in the AccessControl contract.
```solidity
    constructor(address ownerAddress, address treasuryAddress_, address contributorAddress)
        ERC20("Neuron", "NRN")
    {
        _ownerAddress = ownerAddress;
        treasuryAddress = treasuryAddress_;
        isAdmin[_ownerAddress] = true;
        _setupRole(DEFAULT_ADMIN_ROLE, ownerAddress);
        _mint(treasuryAddress, INITIAL_TREASURY_MINT);
        _mint(contributorAddress, INITIAL_CONTRIBUTOR_MINT);
    }
```